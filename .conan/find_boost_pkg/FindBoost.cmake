# Copied from https://github.com/bincrafters/community/issues/26

function(add_cloned_imported_target dst src)
    add_library(${dst} INTERFACE IMPORTED)
    foreach(name INTERFACE_LINK_LIBRARIES INTERFACE_INCLUDE_DIRECTORIES INTERFACE_COMPILE_DEFINITIONS INTERFACE_COMPILE_OPTIONS)
        get_property(value TARGET ${src} PROPERTY ${name} )
        set_property(TARGET ${dst} PROPERTY ${name} ${value})
    endforeach()
endfunction()

function(uppercase_at_pos dst src pos)
    math(EXPR pos_1 ${pos}+1)
    string(SUBSTRING ${src} 0 ${pos} head)
    string(SUBSTRING ${src} ${pos} 1 c)
    string(SUBSTRING ${src} ${pos_1} -1 tail)
    string(TOUPPER ${c} c)
    set(${dst} ${head}${c}${tail} PARENT_SCOPE)
endfunction()

function(uppercase_words dst src)
    uppercase_at_pos(res ${src} 0)
    set(pos 0)
    set(remaining ${src})
    string(FIND ${remaining} "_" rpos)
    while(rpos GREATER_EQUAL 0)
        math(EXPR rpos ${rpos}+1)
        string(SUBSTRING ${remaining} ${rpos} -1 remaining)
        math(EXPR pos ${pos}+${rpos})
        uppercase_at_pos(res ${res} ${pos})
        string(FIND ${remaining} "_" rpos)
    endwhile()
    set(${dst} ${res} PARENT_SCOPE)
endfunction()

foreach(component ${Boost_FIND_COMPONENTS})
    if(NOT TARGET Boost::${component})
        uppercase_words(Compontent ${component})
        add_cloned_imported_target(Boost::${component} CONAN_PKG::Boost.${Compontent})
    endif()
endforeach()
set(Boost_FOUND TRUE)	
